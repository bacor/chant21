// An Arpeggio PEG grammar for gabc_file
//
// Inspired by the PEG grammar used in 'gabc-converter'; see
// https://github.com/saybaar/gabc-converter
//
// Author: Bas Cornelissen
//
// To do
// ----- 
// * Consider adopting the Gregorio terminology of neumes and neumatic cuts
//   see http://gregorio-project.github.io/structure.html
// * Braces above [ob:..]
// * Guide? e.g. f+ (22x)
// * Allow semicolon in attribute values

// FILE
    gabc_file = header separator body EOF
    separator = r'(\n)*%%(\n)+'

// HEADER
    header = attribute*
    attribute = attribute_key r':[ ]*' attribute_value r';(\n)*'
    attribute_key = r'[^:;%]+'
    attribute_value = r'[^%;]+(;\ [^%;]+)*'
        // Allow the use of ; when followed by a space

// BODY
    body = (word (whitespace / EOF))*
    word = syllable+
    syllable = text? "(" music ")"
    whitespace = r'[ \n\r\t\f\v]+'
    // text cannot start with a space
    text = r'[^ \n\r\t\f\v\(][^\(]*'

// MUSIC
    music = (clef / note / barline / spacer / end_of_line / polyphony)*
    clef = r'(c|f)b?[1-4]'
    barline = r",0|,_|,|'|`|::|:\?|:|;[1-6]|;"
    spacer = r'\!|@|//|/0|/\[-?[0-9]\]|/| '

    note = prefix* position suffix*
    prefix = '-'
    suffix = neume_shape? alteration? rhythmic_sign? empty_note_or_accent*
    position = r'[a-mA-M]'
    neume_shape = r'~|>|<|v|V|o(~|<)?|w|s<?|q|0|1'
        // for 'q', see gregorio-project.github.io/gabc/details.html#podatus
    alteration = r'x|y|#'
    rhythmic_sign = r'\.\.?|\'(0|1)?|_[0-5]*'
    empty_note_or_accent = r'r[0-5]?|R'
    end_of_line = r'[zZ]0?'

    polyphony = '{' note+ '}'