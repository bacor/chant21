// An Arpeggio PEG grammar for gabc_file
//
// Inspired by the PEG grammar used in 'gabc-converter'; see
// https://github.com/saybaar/gabc-converter
//
// Author: Bas Cornelissen
//
// To do
// ----- 
// * In GregoBase, there are a number of chants containing "/v" or "v" before
//   a note: e.g. "DÃ©(*vfe/gh)" (id 3974). This breaks the parser (16x) but
//   is this correct syntax?

// FILE
file = (header separator)* body EOF
separator = r'(\n)*%%(\n)+'

// HEADER
header = attribute*
attribute = attribute_key r':[ ]*' attribute_value r';(\n)*'
attribute_key = r'[^:;%]+'
// Allow the use of ; when followed by a space
attribute_value = r'[^%;]*(;\ [^%;]+)*'

// BODY
body = ((macro / word) (whitespace / EOF))*
word = syllable+
syllable = text? "(" music ")"
whitespace = r'[ \n\r\t\f\v]+'
music = (
    custos
    / clef 
    / pausa 
    / alteration 
    / note 
    / spacer 
    / polyphony 
    / brace 
    / code 
    / choral_sign 
    / translation 
    / end_of_line)*

// TEXT
text = (r'[^\(<\* ]*' (annotation / tag) r'[^\(]*') / r'[^ \n\r\t\f\v\(][^\(\n]*'

// TODO include whitespace in annotation
annotation = repeat / psalm / TP / V / R / A / star / '+' / latex
    V = r'<sp>V/</sp>\.?'
    R = r'<sp>R/</sp>\.?'
    A = r'<sp>A/</sp>\.?'
    star = '<c>'? r'\*+' '</c>'?
    repeat = '<i>' (r'i+j?\.?' / r'((R|r)epea?t[a-z]*)[\.? :]*') '</i>'
    psalm = '<i>' r'Ps[ \.~0-9]*' '</i>'
    TP = '<i>' r'[ ]?T[\. ]+P[\. ]*' '</i>'
    latex = '<v>' r'[^<]+' '</v>'

// boldface, smallcaps, mono and ul mostly contains sung text
tag = italic / bold / smallcaps / monospace / underlined
    italic = '<i>' r'[^\<]*' '</i>'
    bold = '<b>' r'[^\<]*' '</b>'
    smallcaps = '<b>' r'[^\<]*' '</b>'
    monospace = '<tt>' r'[^\<]*' '</tt>'
    underlined = '<ul>' r'[^\<]*' '</ul>'
    // TODO how about Special symbols? eg <sp>ae</sp>
    // TODO how about Cocde symbols? eg <c>*</c> seems common

// MUSICAL ELEMENTS
clef = r'(c|f)b?[1-4]'
pausa = pausa_finalis / pausa_major / pausa_minor / pausa_minima
    pausa_minima = r',[_0-6]?' / '`'
    pausa_minor = r';[1-6]?'
    pausa_major = r':[\?\']?'
    pausa_finalis = '::'
// Suffixes on alterations make no sense but sometimes occur
// TODO: check if these are not typos
alteration = position r'x|y|#' suffix? 
spacer = r'\!|@|//|/0|/\[-?[0-9]\]|/| '
note = prefix* position suffix*
    position = r'[a-mA-M]'
    prefix = '-'
    suffix = neume_shape? rhythmic_sign? empty_note_or_accent*
        neume_shape = r'~|>|<|v|V|o(~|<)?|w|s<?|q|0|1'
            // for 'q', see gregorio-project.github.io/gabc/details.html#podatus
        rhythmic_sign = r'\.\.?|\'(0|1)?|_[0-5]*'
        empty_note_or_accent = r'r[0-5]?|R'

// ADVANCED
polyphony = '{' (alteration / note)+ '}'
brace = r'\[(o|u)(b|cba|cb):(0|1)(({|})|(;\d+(\.\d+)?mm))?\]'
choral_sign = r'\[cs:[^\]]+\]'
translation = r'\[alt:[^\]]+\]'
custos = r'[a-m]?\+'
end_of_line = r'[zZ](0|-)?'
    // TODO is z- proper gabc? GregoBase chant 8748 contains it for example
code = "[" ( macro_reference / verbatim_code ) "]"
    macro_reference = r'(n|g|e)m[0-9]'
    verbatim_code = r'(n|g|e)v:[^\]]+'
    macro = r"def-m[0-9]:[^;]+;"